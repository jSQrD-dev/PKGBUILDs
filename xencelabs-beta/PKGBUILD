# Maintainer: jSQrD <n1irdwd5 at aur dot addy dot io>

# Maintainer for the AUR xencelabs package:
## Maintainer: Eric Engestrom <aur@engestrom.ch>

_TARBALL_PKGVER=1.3.4-26
_UPLOAD_DATE=20240729
_ORIGINAL_PKGNAME=xencelabs

pkgname=xencelabs-beta
pkgver=${_TARBALL_PKGVER/-/.}

pkgrel=1
pkgdesc="Beta driver for the Xencelabs Pen Tablets and Pen Displays"
arch=('x86_64')
url="https://xencelabs.com"
license=(custom:xencelabs)
depends=('qt5-base' 'qt5-svg' 'qt5-x11extras')
provides=('xencelabs')
conflicts=('xencelabs')
install='xencelabs.install'
source=(
  https://download01.xencelabs.com/file/${_UPLOAD_DATE}/Xencelabslinux_${_TARBALL_PKGVER}-RC.zip
)
sha256sums=(
  cb8b129447141497a3aedcc3885505fa41b0477c8d73b930b3173b14b227bfe0
)

prepare() {
  rm "${srcdir}/20240724更新Linux Xencelabs驱动/xencelabs-1.3.4-26.rpm"
  rm "${srcdir}/20240724更新Linux Xencelabs驱动/xencelabs1.3.4-26.deb"
  tar -xf "${srcdir}/20240724更新Linux Xencelabs驱动/xencelabs-1.3.4-26.tar.gz"
}

build() {
  cd "$_ORIGINAL_PKGNAME-$_TARBALL_PKGVER/App" || exit

  # fix FSH to match Arch's
  mv lib/*/ usr/lib/
  rmdir lib

  # Replace xencelabs.desktop copy with symlink
  ln -sf ../../../usr/share/applications/xencelabs.desktop \
    etc/xdg/autostart/xencelabs.desktop

  sed -i 's|Exec=/usr/lib/xencelabs/start.sh|Exec=/usr/bin/xencelabs|' \
    "usr/share/applications/xencelabs.desktop"
}

package() {
  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/lib/xencelabs"
  mkdir -p "${pkgdir}/etc"

  install -Dm644 "$_ORIGINAL_PKGNAME-$_TARBALL_PKGVER/App/usr/lib/xencelabs/license" "$pkgdir/usr/share/licenses/$_ORIGINAL_PKGNAME/LICENSE"

  cp -r "$_ORIGINAL_PKGNAME-$_TARBALL_PKGVER/App/etc/" "${pkgdir}/"

  cp -r "$_ORIGINAL_PKGNAME-$_TARBALL_PKGVER/App/usr/lib/xencelabs/config" "${pkgdir}/usr/lib/xencelabs"
  install -Dm644 \
    "$_ORIGINAL_PKGNAME-$_TARBALL_PKGVER/App/usr/share/applications/$_ORIGINAL_PKGNAME.desktop" \
    "${pkgdir}/usr/share/applications/xencelabs.desktop"
  install -Dm644 \
    "$_ORIGINAL_PKGNAME-$_TARBALL_PKGVER/App/usr/share/icons/xencelabs.png" \
    "${pkgdir}/usr/share/icons/xencelabs.png"

  install -D \
    "$_ORIGINAL_PKGNAME-$_TARBALL_PKGVER/App/usr/lib/xencelabs/xencelabs" \
    "${pkgdir}/usr/bin/xencelabs"
  install -Dm644 \
    "$_ORIGINAL_PKGNAME-$_TARBALL_PKGVER/App/usr/lib/udev/rules.d/10-xencelabs.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/10-xencelabs.rules"

  chmod +x "$pkgdir"/usr/bin/xencelabs
}
